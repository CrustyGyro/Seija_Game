#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Nareko_Spell_03"]
//#Background["./Stage/STG_03/STG_BG.dnh"];
#Player[ "./Player/PL_Reimu.dnh" ]
#System["./System/System_Main_Hud.dnh"]

//--------------------------------------------------Variables------------------------------------------------------

let bossObj;
let objScene = GetEnemyBossSceneObjectID();
let bossX = 0;
let bossY = 0;

int Dir_Switch = 1;

//SetIntersectionVisualization( true );
let CutinTex = GetCurrentScriptDirectory() ~ "./Resource/Textures/Boss/Cutin/CurseOfRa.png";

//---------------------------------------------------Include-------------------------------------------------------

	#include "./lib/Index_Boss.dnh"
	#include "./lib/Lib_Spell.dnh"
	#include "./lib/Lib_SpellCutin.dnh"

//----------------------------------------------------Start--------------------------------------------------------

@Loading {
	
    LoadTexture( CutinTex );

}


@Initialize {

	bossObj = ObjEnemy_Create(OBJ_ENEMY_BOSS);	
	
	ObjEnemy_Regist( bossObj );	

	T_Lifebar( bossObj );
	T_HitEffect( bossObj );
	ObjEnemy_SetDamageRate( bossObj, 0, 0 );	

	Init_Boss_Nareko( bossObj );
	
	ObjMove_SetDestAtWeight( bossObj, GetStgFrameWidth/2, Get_CenterY/2 + 16, 15, 8 );
	
	
	//SetPlayerLife( 8 );
	T_Main;
	T_End;
	
}

@Event { 

	alternative(GetEventType()) 
	case( EV_REQUEST_LIFE ) { 
		SetScriptResult( 2000 ); // 3000-4500 For Nons ( At 100% Damage Rate. )
	}
	case( EV_REQUEST_TIMER ) { 
		//SetScriptResult( 50 );
		SetScriptResult( 65 );
	}	
	case( EV_REQUEST_SPELL_SCORE ){
       SetScriptResult( 5000000 );
    }	



} 

@MainLoop {

	bossX = ObjMove_GetX( bossObj );
	bossY = ObjMove_GetY( bossObj );
	
	SetCommonData( "Effect_X", bossX );
	SetCommonData( "Effect_Y", bossY );	

	ObjEnemy_SetIntersectionCircleToShot( bossObj, bossX, bossY, 16 );
	ObjEnemy_SetIntersectionCircleToPlayer( bossObj, bossX, bossY, 8 );
	
	yield;
}

@Finalize {
} 


task T_InitSpell(){
	
	//let CutinTex = GetCurrentScriptDirectory() ~ "./Resource/Textures/Boss/Cutin/CurseOfRa.png";
	
	T_CallSpell_A1( "Obstructing Sign: \"Winding Tomb's Passage\"", 0, "Normal", objScene );
	T_Call_Spell_Cutin( CutinTex, [ 0, 0, 200, 384 ], 1, CC_ORANGE );	
	
	ObjEnemyBossScene_StartSpell( objScene );
	
	yield;
	T_SpellBG_Nareko();	
	
}	


//----------------------------------------------------Patterns--------------------------------------------------------

task T_Main {


	//NotifyEventAll( EV_BOSS_EFFECT, bossObj, CC_RED );

	wait( 125 );

	T_InitSpell();
	
	wait( 160 );

	ObjEnemy_SetDamageRate( bossObj, 0.3, 0.8 );
	
	T_Boss_Shot_C;
	
}	


task T_Boss_Shot_C{
	
	float Angle_I = 90;
	int Count_X = 4;
	int Count_Y = 5;
	int Shot_go = -1;
	int Delay_Pyramid = 500;
	
	//A mess....
	
	int[] Laser_Color = [ SHOT_LASER_YELLOW, SHOT_LASER_BLUE, SHOT_LASER_YELLOW, SHOT_LASER_BLUE, SHOT_LASER_YELLOW, SHOT_LASER_BLUE ];
	int[] Connector_Color = [ SHOT_CLOUD_YELLOW, SHOT_CLOUD_BLUE, SHOT_CLOUD_YELLOW, SHOT_CLOUD_BLUE, SHOT_CLOUD_YELLOW, SHOT_CLOUD_BLUE ];
	int[] Core_Color = [ SHOT_ORB_YELLOW, SHOT_ORB_BLUE, SHOT_ORB_YELLOW, SHOT_ORB_BLUE, SHOT_ORB_YELLOW, SHOT_ORB_BLUE ];
	int[] Shot_Color = [ SHOT_DIAMOND_INVERT_DYELLOW, SHOT_DIAMOND_INVERT_BLUE, SHOT_DIAMOND_INVERT_DYELLOW, SHOT_DIAMOND_INVERT_BLUE, SHOT_DIAMOND_INVERT_DYELLOW, SHOT_DIAMOND_INVERT_BLUE ];
	
	if( ObjEnemy_GetInfo( bossObj, INFO_LIFE ) > 0 ){
		T_Charge_A1( bossObj, 32, "Maple", CC_ORANGE );
		T_PlaySE( SE_CHARGE_00, 35 );		
	}			
	
	wait( 100 );
	
	while( ObjEnemy_GetInfo( bossObj, INFO_LIFE ) > 0 ){
		ascent( X in 0..Count_X ){

			ascent( Y in 0..Count_Y ){
				
				if( ObjEnemy_GetInfo( bossObj, INFO_LIFE ) > 0 ){
					T_PlaySE( SE_CHIME_01, 36 );
					
					T_Create_Pyramid_A( 40 + X * Get_MaxX/Count_X, 40 + Y * Get_MaxY/Count_Y, Angle_I, 40, ( Y + X ) * 8*2, Shot_go, Laser_Color[ Y ], Connector_Color[ Y ], Core_Color[ Y ], Shot_Color[ Y ] );
					
					Angle_I = -Angle_I;	
				
					wait( 8 );
				}
			}
		}	

		wait( 100 );

		Shot_go = -Shot_go;	
		ObjMove_SetDestAtWeight( bossObj, GetStgFrameWidth/2 + rand( -32, 32 ), Get_CenterY/2 + 16 + rand( -8, 8 ), 16, 1 );	
		
		wait( Delay_Pyramid );
		
		if( ObjEnemy_GetInfo( bossObj, INFO_LIFE ) > 0 ){
			T_Charge_A1( bossObj, 32, "Maple", CC_ORANGE );
			T_PlaySE( SE_CHARGE_00, 35 );		
		}			
			
		wait( 100 );
		
		//Angle_I = -Angle_I;
		
		ascent( X in 0..Count_X ){

			ascent( Y in 0..Count_Y ){
				
				if( ObjEnemy_GetInfo( bossObj, INFO_LIFE ) > 0 ){
				
					T_PlaySE( SE_CHIME_01, 36 );
					
					T_Create_Pyramid_A( Get_MaxX - 40 - X * Get_MaxX/Count_X, 40 + Y * Get_MaxY/Count_Y, Angle_I, 40, ( Y + X ) * 8*2, Shot_go, Laser_Color[ Y ], Connector_Color[ Y ], Core_Color[ Y ], Shot_Color[ Y ] );
					
					Angle_I = -Angle_I;
					
					wait( 8 );
				}
			}
		}	

		wait( 100 );

		Shot_go = -Shot_go;	
		ObjMove_SetDestAtWeight( bossObj, GetStgFrameWidth/2 + rand( -32, 32 ), Get_CenterY/2 + 16 + rand( -8, 8 ), 16, 1 );			
		
		wait( Delay_Pyramid );
		
		if( ObjEnemy_GetInfo( bossObj, INFO_LIFE ) > 0 ){
			T_Charge_A1( bossObj, 32, "Maple", CC_ORANGE );
			T_PlaySE( SE_CHARGE_00, 35 );		
		}			
			
		wait( 100 );
		
		//Angle_I = -Angle_I;
		
		yield;


	}
	
	task T_Create_Pyramid_A( X, Y, Init_Angle, Radius, ID, Go_Status, Laser_Color, Connector_Color, Core_Color, Shot_Color ){

		float Delay = 0;
		int Way = 3;
		int Stack = 1;	
		
		int ShotObj_Core = ShotA1( X, Y, 0, 90, Core_Color, 60 );
		int[] Laser_Connector = Pattern_Shot_A1( X, Y, 0, 32, Init_Angle, 0, Connector_Color, PATTERN_RING, Way, Stack, 60, Connector_Color );
		
		for( int i = 0; i < Way * Stack; i++ ) {
			
			T_Manage_Pyramid( ShotObj_Core, Laser_Connector[i] );
			if( i < Way-1 ){
				
				T_Manage_Laser( Laser_Connector[i], Laser_Connector[i+1] );
			}
			else{
				
				T_Manage_Laser( Laser_Connector[Way-1], Laser_Connector[0] );

			}	

		}
		
		task T_Manage_Laser( Shot, Next_Shot ){
			
			let Connector = CreateStraightLaserA1(  ObjMove_GetX( Shot ), ObjMove_GetY( Shot ), 0, 0, 16, 99999, Laser_Color, 80 );
			ObjRender_SetAlpha( Connector, 0 );
			ObjStLaser_SetSource( Connector, false );
			
			async{
				
				yield;
				
				ObjRender_SetAlpha( Connector, 255 );
				
				while( !Obj_IsDeleted( Shot ) || !Obj_IsDeleted( Next_Shot ) ){

						
					if( Obj_IsDeleted( Next_Shot ) ){ break; }
					if( Obj_IsDeleted( Shot ) ){ break; }
			
						
					ObjMove_SetPosition( Connector, ObjMove_GetX( Shot ), ObjMove_GetY( Shot ) ); 
					ObjStLaser_SetAngle( Connector, dottheta( ObjMove_GetX( Shot ), ObjMove_GetY( Shot ), ObjMove_GetX( Next_Shot ), ObjMove_GetY( Next_Shot ) ) );
					ObjLaser_SetLength( Connector, distance( ObjMove_GetX( Shot ), ObjMove_GetY( Shot ), ObjMove_GetX( Next_Shot ), ObjMove_GetY( Next_Shot ) ) + 1 );	
						
					yield;
				}
				Obj_Delete( Connector );
				
			}	
			
			async{
				
				//wait( 200 - ID );
				while( Shot_go == Go_Status ){ yield; }
				
				float Laser_Angle = ObjStLaser_GetAngle( Connector );
				float[] Shot_Speed = [ 0.2, 0.25, 0.25, 0.2, 0.1, 0.1, 0.1 ];
				
				T_PlaySE( SE_CHIME_00, 36 );
				
				if( ObjEnemy_GetInfo( bossObj, INFO_LIFE ) > 0 ){
					ascent( a in 0..4 ){
						
						float Shot_X = ObjMove_GetX( Connector ) + a * ObjLaser_GetLength( Connector )/4 * cos( Laser_Angle );
						float Shot_Y = ObjMove_GetY( Connector ) + a * ObjLaser_GetLength( Connector )/4 * sin( Laser_Angle );
						
						int ShotObj_A = ShotA2( Shot_X, Shot_Y, 0, ObjStLaser_GetAngle( Connector ) - 90, Shot_Color, 20, Shot_Color );
						int ShotObj_B = ShotA2( Shot_X, Shot_Y, 0, ObjStLaser_GetAngle( Connector ) - 90, SHOT_BALL_AURA_DYELLOW, 1, SHOT_CLOUD_YELLOW );
						
						ObjShot_SetDeleteFrame( ShotObj_B, 24 );
						Obj_SetRenderPriorityI( ShotObj_B, Obj_GetRenderPriorityI( ShotObj_B )+1 );
						ObjRender_SetBlendType( ShotObj_B, BLEND_ADD_ARGB  );
						
						ObjMove_AddPatternA2( ShotObj_A, 80, NO_CHANGE, NO_CHANGE, 0.01, 0.1 + Shot_Speed[a], 0 );
						//ObjMove_AddPatternA2( ShotObj_A, 30, NO_CHANGE, NO_CHANGE, 0.01, 0.3 - a/8 * sin( a*2 ), 0 );
						//ObjMove_AddPatternA2( ShotObj_A, 160, NO_CHANGE, NO_CHANGE, 0.01, 1 - a/8 * sin( a*2 ), 0 );

					}
				}				
				
				Obj_Delete( Shot );
				Obj_Delete( Next_Shot );
				Obj_Delete( Connector );		

				async{
					//wait( 200 + ID*5.5 );
					
					wait( 160 + ID );
					
					if( !Obj_IsDeleted( ShotObj_Core ) ){
						
						T_PlaySE( SE_LASER_03, 36 );
						
						//int Aimed_ShotObj_A = ShotA2( ObjMove_GetX( ShotObj_Core ), ObjMove_GetY( ShotObj_Core ), 0.5, GetAngleToPlayer( ShotObj_Core ), SHOT_ORB_YELLOW, 20, SHOT_ORB_YELLOW );
						ascent( c in 0..4 ){
							
							let Aimed_Laser = Laser_A1( ObjMove_GetX( ShotObj_Core ), ObjMove_GetY( ShotObj_Core ), 45 - c * 360/4, 512, 8, 60, Laser_Color, 60 );
							ObjStLaser_SetSource( Aimed_Laser, false );
							
						}	
						//let Aimed_Laser = CreateStraightLaserA1( ObjMove_GetX( ShotObj_Core ), ObjMove_GetY( ShotObj_Core ), GetAngleToPlayer( ShotObj_Core ), 512, 24, 20, SHOT_LASER_YELLOW, 30 );
						
						async{
							
							wait( 60 );
							
							ObjShot_SetIntersectionEnable( ShotObj_Core, false );
							
							T_PlaySE( SE_LASER_00, 36 );
							
							ascent( a in 0..60 ){
								ObjRender_SetScaleXYZ( ShotObj_Core, 1 - a * 1/60, 1 - a * 1/60, 1 - a * 1/60 );
								yield;
							}
							
							Obj_Delete( ShotObj_Core );
							
						}	
						
						//wait( 180 );
						
						//Obj_Delete( ShotObj_Core );
					}
				}	
				
			}	
			
		}	

		task T_Manage_Pyramid( Parent, Shot ){
			
			async{
				
				float Angle = dottheta( ObjMove_GetX( Shot ), ObjMove_GetY( Shot ), ObjMove_GetX( Parent ), ObjMove_GetY( Parent ) );
				float Frame = 0;
				float Inital_Radius = 0;
			//	float Angle_Init = Init_Angle;
				
				ascent( i in 0..60 ){
					
					float X_Pos = ObjMove_GetX( Parent ) + Inital_Radius * cos( Angle );
					float Y_Pos = ObjMove_GetY( Parent ) + Inital_Radius * sin( Angle );					
								
					//Angle = Interpolate_Overshoot( Angle_Init, Init_Angle, 2, i/60 );			
					Inital_Radius = Interpolate_Overshoot( 0, Radius, 2, i/60 );				
									
					ObjMove_SetPosition( Shot, X_Pos, Y_Pos ); 
					ObjMove_SetAngle( Shot, Angle );
					
					yield;
					
				}	
		
			}
			
		}	

	}		
	
	yield;
	
}	

task T_Boss_Shot_B{
	
	int ShotAngle = 0;
	int SubShot_Angle = 24;

	async{
		
		while( ObjEnemy_GetInfo( bossObj, INFO_LIFE ) > 0 ){
			
			if( ObjEnemy_GetInfo( bossObj, INFO_LIFE ) > 0 ){
				ascent( a in 0..10 ){

					ascent( b in 0..2 ){	
					
						T_PlaySE( SE_CHIME_02, 35 );
						int ShotObj_A = ShotA1( bossX + rand( -16, 16 ) , bossY + rand( -16, 16 )- 64, 3, ( 90 + a * 360/10 ) + ShotAngle, SHOT_DOT_DORANGE, 10 );
							//int ShotObj_A = ShotA1( bossX, bossY - 64, 2.4, ( 90 + a * 360/10 ) + ShotAngle, SHOT_DOT_DORANGE, 10 );
						ObjMove_AddPatternA2( ShotObj_A, 60, NO_CHANGE, NO_CHANGE, -0.08, 0, 0 );
						ObjMove_AddPatternA2( ShotObj_A, 90, NO_CHANGE, ObjMove_GetAngle( ShotObj_A ) + SubShot_Angle, 0.01, rand( 0.5, 2 ), 0 );
							
						//ObjRender_SetBlendType( ShotObj_A, BLEND_ADD_ARGB  );
						SubShot_Angle = -SubShot_Angle;
					}	//yield;
					
				}	
			}
			
			ShotAngle +=10;
			
			wait( 15 );
		}
	}	
}
//-----------------------------------------------------Death---------------------------------------------------------

task T_End {
	
    while( ObjEnemy_GetInfo( bossObj, INFO_LIFE ) > 0 ) {
	     yield;
	}
	
	if( ObjEnemyBossScene_GetInfo( GetEnemyBossSceneObjectID, INFO_IS_LAST_STEP ) ){

		NotifyEventAll( EV_CLEAR_ENEMY_SHOTS, GetPlayerObjectID(), 60, 12 );
		
		yield;
		
		SetAutoDeleteObject(true);
		T_Boss_Shotdown( bossObj, "Petal", CC_RED );
		wait( 200 );
		
		Obj_Delete( bossObj );	
		
		ENEMY_CLEAR = true;
		
		yield;
		
		ENEMY_CLEAR = false;
		
		wait( 800 );
		
		CloseScript( GetOwnScriptID() );
		
	}	
	else{
	
	
		NotifyEventAll( EV_CLEAR_ENEMY_SHOTS, GetPlayerObjectID(), 60, 12 );
		//DeleteShotAll( TYPE_ALL, TYPE_ITEM );
		
		yield;
		
		SetAutoDeleteObject( true );
		
		Obj_Delete( bossObj );	
		
		wait( 60 );
		
		//DeleteShotAll(TYPE_ALL, TYPE_ITEM);
		
		CloseScript( GetOwnScriptID() );
	
	}
	
	//SetAutoDeleteObject(true);
	
	//Obj_Delete( bossObj );	
	//CloseScript( GetOwnScriptID() );
	
}

