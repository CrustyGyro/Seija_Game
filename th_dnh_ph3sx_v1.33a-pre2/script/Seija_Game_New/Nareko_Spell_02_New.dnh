#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Nareko_Spell_02_New"]
//#Background["./Stage/STG_03/STG_BG.dnh"];
#Player[ "./Player/PL_Reimu.dnh" ]
#System["./System/System_Main_Hud.dnh"]

//--------------------------------------------------Variables------------------------------------------------------

let bossObj;
let objScene = GetEnemyBossSceneObjectID();
let bossX = 0;
let bossY = 0;

//SetIntersectionVisualization( true );
let CutinTex = GetCurrentScriptDirectory() ~ "./Resource/Textures/Boss/Cutin/CurseOfRa.png";

//---------------------------------------------------Include-------------------------------------------------------

	#include "./lib/Index_Boss.dnh"
	#include "./lib/Lib_Spell.dnh"
	#include "./lib/Lib_SpellCutin.dnh"

//----------------------------------------------------Start--------------------------------------------------------

@Loading {
	
    LoadTexture( CutinTex );

}


@Initialize {

	bossObj = ObjEnemy_Create(OBJ_ENEMY_BOSS);	
	
	ObjEnemy_Regist( bossObj );	

	T_Lifebar( bossObj );
	T_HitEffect( bossObj );
	ObjEnemy_SetDamageRate( bossObj, 0, 0 );	

	Init_Boss_Nareko( bossObj );
	
	ObjMove_SetDestAtWeight( bossObj, GetStgFrameWidth/2, Get_CenterY/2 + 16, 15, 8 );
	
	
	//SetPlayerLife( 8 );
	T_Main;
	T_End;
	
}

@Event { 

	alternative(GetEventType()) 
	case( EV_REQUEST_LIFE ) { 
		SetScriptResult( 2000 ); // 3000-4500 For Nons ( At 100% Damage Rate. )
	}
	case( EV_REQUEST_TIMER ) { 
		//SetScriptResult( 50 );
		SetScriptResult( 65 );
	}	
	case( EV_REQUEST_SPELL_SCORE ){
       SetScriptResult( 5000000 );
    }	



} 

@MainLoop {

	bossX = ObjMove_GetX( bossObj );
	bossY = ObjMove_GetY( bossObj );
	
	SetCommonData( "Effect_X", bossX );
	SetCommonData( "Effect_Y", bossY );	

	ObjEnemy_SetIntersectionCircleToShot( bossObj, bossX, bossY, 16 );
	ObjEnemy_SetIntersectionCircleToPlayer( bossObj, bossX, bossY, 8 );
	
	yield;
}

@Finalize {
} 


task T_InitSpell(){
	
	//let CutinTex = GetCurrentScriptDirectory() ~ "./Resource/Textures/Boss/Cutin/CurseOfRa.png";
	
	//T_CallSpell_A1( "Obstructing Sign: \"Winding Labyrinth\"", 0, "Normal", objScene );
	T_CallSpell_A1( "Gold Sign: \"Valley Of Great Kings\"", 0, "Normal", objScene );
	T_Call_Spell_Cutin( CutinTex, [ 0, 0, 200, 384 ], 1, CC_ORANGE );	
	
	ObjEnemyBossScene_StartSpell( objScene );
	
	yield;
	T_SpellBG_Nareko();	
	
}	


//----------------------------------------------------Patterns--------------------------------------------------------

task T_Main {


	//NotifyEventAll( EV_BOSS_EFFECT, bossObj, CC_RED );

	wait( 125 );

	T_InitSpell();
	
	wait( 80 );

	ObjEnemy_SetDamageRate( bossObj, 0.53, 0.8 );
	
	//T_Boss_Shot_B;
	//T_Boss_Shot_C;
	T_Boss_Shot_Sub;
	
	T_PlaySE( SE_CHIME_01, 36 );
	
	/*
	ascent( i in 0..3 ){
		T_Boss_Shot_A( Get_CenterX, -80 + i * Get_MaxY/3, 90, 0, 0 );
		T_Boss_Shot_A( Get_CenterX + 160, Get_MaxY + 80 - i * Get_MaxY/3, -90, 0, 0 );
		T_Boss_Shot_A( Get_CenterX - 160, Get_MaxY + 80 - i * Get_MaxY/3, -90, 0, 0 );
	
	}	
	
	wait( 320 );
	*/
	while( ObjEnemy_GetInfo( bossObj, INFO_LIFE ) > 0 ){
		
		T_PlaySE( SE_CHIME_01, 36 );
		T_Boss_Shot_A( Get_CenterX, -80, 90, 128-32, 1 );
		T_Boss_Shot_A( Get_CenterX + 128 +8 , Get_MaxY + 80, -90, -128 + 32, 1 );
		T_Boss_Shot_A( Get_CenterX - 128 - 8 , Get_MaxY + 80, -90, -128 + 32, 1 );		
	
		wait( 160 );		
		
	}
	
	
}	

task T_Boss_Shot_Sub{
	
	async{
		
		wait( 320 );
		
		while( ObjEnemy_GetInfo( bossObj, INFO_LIFE ) > 0 ){
			
			Obj_SetValue( bossObj,"Cast", 1 );
			
			if( ObjEnemy_GetInfo( bossObj, INFO_LIFE ) > 0 ){
				T_Charge_A1( bossObj, 20, "Maple", CC_ORANGE );
				T_PlaySE( SE_CHARGE_00, 35 );		
			}			
			
			wait( 30 );
			
			float Angle_Laser = GetAngleToPlayer( bossObj );
			
			ascent( a in 0..3 ){
			
				async{
					
					T_PlaySE( SE_LASER_03, 36 );
					wait( 80 );
					T_PlaySE( SE_LASER_02, 26 );
					
				}					
					
				let LaserObj_A = Laser_A1( bossX, bossY - 60, Angle_Laser - a * 8/3, 512, 10, 30, SHOT_LASER_YELLOW, 80 );
				let LaserObj_B = Laser_A1( bossX, bossY - 60, Angle_Laser + a * 8/3, 512, 10, 30, SHOT_LASER_YELLOW, 80 );
		
				wait( 8 );
			}
			
			wait( 160 );
			
			Obj_SetValue( bossObj,"Cast", 0 );
			ObjMove_SetDestAtWeight( bossObj, GetStgFrameWidth/2 + rand( -32, 32 ), Get_CenterY/2 + 16 + rand( -8, 8 ), 16, 1 );	
			
			wait( 320 );
		}
		
	}	
	
	while( ObjEnemy_GetInfo( bossObj, INFO_LIFE ) > 0 ){
		
		T_PlaySE( SE_CHIME_02, 36 );
		
		ascent( a in 0..6 ){
			
			//T_PlaySE( SE_CHIME_02, 36 );
			
			int ShotObj_A = ShotA2( 0 + a * Get_MaxY/6, -32, 0.5, 90, SHOT_FIREBALL_BLUE, 10, SHOT_FIREBALL_BLUE );
			
			wait( 5 );
		}
		wait( 32 );
		
		T_PlaySE( SE_CHIME_02, 36 );
		
		ascent( a in 0..8 ){
			
			//T_PlaySE( SE_CHIME_02, 36 );
			
			int ShotObj_A = ShotA2( -32 + a * Get_MaxY/6, -32, 0.5, 90, SHOT_FIREBALL_BLUE, 10, SHOT_FIREBALL_BLUE );
			
			wait( 5 );
		}	
		wait( 32 );
		
	}
	
}	

task T_Boss_Shot_A( X, Y, Angle, Dir, Speed ){
	
	//int ShotObj_A = ShotA2( X, Y, 5, Angle, SHOT_LARGE_DIAMOND_INVERT_ORANGE, 20, SHOT_LARGE_DIAMOND_INVERT_ORANGE );
	
	int Way = 3;
	int Stack = 1;
	
	int ShotObj_A = ShotA1( X, Y + Dir, Speed, Angle, SHOT_ORB_YELLOW, 20 );
	
	let Pattern_Obj_SpawnEffect = Pattern_Shot_A1( X, Y, 0, 64, Angle, 0, SHOT_ORB_YELLOW, PATTERN_RING, Way, Stack, 20, SHOT_ORB_YELLOW );
	let Pattern_Obj = Pattern_Shot_A1( X, Y, 0, 64, Angle, 0, SHOT_ORB_YELLOW, PATTERN_RING, Way, Stack, 1, SHOT_ORB_YELLOW );
	let Pattern_Obj_B = Pattern_Shot_A1( X, Y, 0, 64, Angle, 0, SHOT_ORB_BLUE, PATTERN_RING, Way, Stack, 1, SHOT_ORB_BLUE );
	
	for( int i = 0; i < Way * Stack; i++ ) {
		
		ObjShot_SetDeleteFrame( Pattern_Obj[i], 320*2 );
		ObjShot_SetDeleteFrame( Pattern_Obj_B[i], 320*2 );
		
		ObjMove_AddPatternA2( Pattern_Obj[i], 10, NO_CHANGE, NO_CHANGE, 0.01, Speed, 0 );
		ObjMove_AddPatternA2( Pattern_Obj_B[i], 10, NO_CHANGE, NO_CHANGE, 0.01, Speed, 0 );		
		//ObjShot_SetDeleteFrame( );
		
		ObjShot_SetDeleteFrame( Pattern_Obj_SpawnEffect[i], 20 );
		ObjMove_SetAngle( Pattern_Obj[i], Angle );
		ObjMove_SetAngle( Pattern_Obj_B[i], Angle );
		ObjShot_SetAutoDelete( Pattern_Obj[i], false );
		ObjShot_SetAutoDelete( Pattern_Obj_B[i], false );		
	}	
	yield;
	
	for( int i = 0; i < Way * Stack; i++ ) {
			
		ObjRender_SetBlendType( Pattern_Obj_B[i], BLEND_SUBTRACT );
		///ObjRender_SetScaleXYZ( Pattern_Obj[i], 1.2, 1.2, 1.2 );
	
		if( i < Way -1 ){
			
			T_ManageShot( Pattern_Obj[i], Pattern_Obj[i+1] );
		}
		else{
			
			T_ManageShot( Pattern_Obj[2], Pattern_Obj[0] );

		}	

	}
	
	task T_ManageShot( Shot, Next_Shot ){

		let Connector = CreateStraightLaserA1(  ObjMove_GetX( Shot ), ObjMove_GetY( Shot ), 0, 64, 18, 99999, SHOT_LASER_YELLOW, 80 );
		let Connector_Dark = CreateStraightLaserA1(  ObjMove_GetX( Shot ), ObjMove_GetY( Shot ), 0, 64, 14, 99999, SHOT_LASER_BLUE, 80 );
		
		
		ObjRender_SetBlendType( Connector_Dark, BLEND_SUBTRACT );
		
		
		T_Manage_Laser( Connector );
		T_Manage_Laser( Connector_Dark );
			
		task T_Manage_Laser( Obj ){	
			while( !Obj_IsDeleted( Shot ) || !Obj_IsDeleted( Next_Shot ) ){
				
				if( Obj_IsDeleted( Next_Shot ) ){ break; }
				if( Obj_IsDeleted( Shot ) ){ break; }
				
				ObjMove_SetPosition( Obj, ObjMove_GetX( Shot ), ObjMove_GetY( Shot ) ); 
				ObjStLaser_SetAngle( Obj, dottheta( ObjMove_GetX( Shot ), ObjMove_GetY( Shot ), ObjMove_GetX( Next_Shot ), ObjMove_GetY( Next_Shot ) ) );
				ObjLaser_SetLength( Obj, distance( ObjMove_GetX( Shot ), ObjMove_GetY( Shot ), ObjMove_GetX( Next_Shot ), ObjMove_GetY( Next_Shot ) ) + 1 );			
				
				
				
				yield;
			}
			Obj_Delete( Obj );	
		}
	}	

}	


//-----------------------------------------------------Death---------------------------------------------------------

task T_End {
	
    while( ObjEnemy_GetInfo( bossObj, INFO_LIFE ) > 0 ) {
	     yield;
	}
	
	if( ObjEnemyBossScene_GetInfo( GetEnemyBossSceneObjectID, INFO_IS_LAST_STEP ) ){

		NotifyEventAll( EV_CLEAR_ENEMY_SHOTS, GetPlayerObjectID(), 60, 12 );
		
		yield;
		
		SetAutoDeleteObject(true);
		T_Boss_Shotdown( bossObj, "Petal", CC_RED );
		wait( 200 );
		
		Obj_Delete( bossObj );	
		
		ENEMY_CLEAR = true;
		
		yield;
		
		ENEMY_CLEAR = false;
		
		wait( 800 );
		
		CloseScript( GetOwnScriptID() );
		
	}	
	else{
	
		NotifyEventAll( EV_SPAWN_ITEM_NON, bossX, bossY, 30, 64 );	
		NotifyEventAll( EV_CLEAR_ENEMY_SHOTS, GetPlayerObjectID(), 60, 12 );
		//DeleteShotAll( TYPE_ALL, TYPE_ITEM );
		
		yield;
		
		SetAutoDeleteObject( true );
		
		Obj_Delete( bossObj );	
		
		wait( 60 );
		
		//DeleteShotAll(TYPE_ALL, TYPE_ITEM);
		
		CloseScript( GetOwnScriptID() );
	
	}
	
	//SetAutoDeleteObject(true);
	
	//Obj_Delete( bossObj );	
	//CloseScript( GetOwnScriptID() );
	
}

